plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "fr.stardustenterprises.rust.importer" version "3.2.5"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

dependencies {
    rust(project(":reqwest-jni"))
}

// javac -h
tasks.register('generateJniHeaders', JavaCompile) {
    classpath = sourceSets.main.compileClasspath
    destinationDir file("${layout.buildDirectory}/generated/jni")
    source = sourceSets.main.java
    options.compilerArgs += [
            '-h', file("${layout.buildDirectory}/generated/jni"),
            '-d', file("${layout.buildDirectory}/generated/jni-classes"),
    ]
    doLast {
        delete file("${layout.buildDirectory}/generated/jni-classes")
    }
}

rustImport {
    baseDir = "/META-INF/natives"
    layout = "hierarchical"
}

java {
    withSourcesJar()
    withJavadocJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

signing {
    sign publishing.publications
}

group = 'rocks.kavin'
version = '1.0.13'

publishing {
    repositories {
        maven {
            name = "sonatypeStaging"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty("ossrh.user") ?: System.getenv("USERNAME")
                password = project.findProperty("ossrh.pass") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        ossrh(MavenPublication) {
            from(components.java)
            pom {
                name = 'reqwest4j'
                packaging 'jar'
                description = 'Java bindings to Reqwest with jni-rs for use in Piped\'s backend!'
                url = 'https://github.com/TeamPiped/reqwest4j'

                scm {
                    connection = 'scm:git:git://github.com/TeamPiped/reqwest4j.git'
                    developerConnection = 'scm:git:https://github.com/TeamPiped/reqwest4j.git'
                    url = 'https://github.com/TeamPiped/reqwest4j'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/TeamPiped/reqwest4j/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'kavin'
                        name = 'Kavin'
                        email = 'kavin@kavin.rocks'
                    }
                }
            }
        }
    }
}
